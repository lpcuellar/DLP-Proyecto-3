COMPILER MyCOCOR
(.//Especificacion de Funciones.)
CHARACTERS

letter= "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
digit= "0123456789".

cr = CHR(13).
lf = CHR(10).
tab = CHR(9).
ignore = cr+lf+tab.
comillas = CHR(34).
stringletter = ANY-comillas-ignore.

operadores ="+-=()[]{}|.<>".
MyANY = ANY-operadores.

TOKENS

ident = letter{letter|digit} EXCEPT KEYWORDS.
string = comillas stringletter{stringletter} comillas.

char = "'"["/"]letter"'".
charnumber = "CHR("digit{digit}")".
charinterval = "CHR("digit{digit}")""..""CHR("digit{digit}")".

nontoken = MyANY.
startcode ="(.".
endcode=".)".

IGNORE SET ignore

PRODUCTIONS

MyCOCOR= 				(.CompilerName = ' '
					  EndName = ' '.)
	"COMPILER" 
	Ident<CompilerName>		(.print("Nombre Inicial del Compilador:{0}",CompilerName).) 
	[Codigo]
	Body
	"END" Ident<EndName>	(.print("Nombre Final del Compilador:{0}",EndName).) 
.

Body = 
	Characters 
	[Keywords] 
	Tokens 
	Productions  	
.


Characters =				(.CharName = ' '
					   Counter = 0.)

	"CHARACTERS"			(.print("LEYENDO CHARACTERS").)
	{
		Ident<CharName>	(.Counter+=print("Char Set {0}: {1}",Counter,CharName).)
		"="
		CharSet			
		{
			"+"CharSet
			|
			"-"CharSet
		}
		"."
	}
.
			 
Keywords = 				(.KeyName = ' '
					  StringValue = ' '
    					  Counter = 0.)

	"KEYWORDS"			(.print("LEYENDO KEYWORDS").)
	{
		
		Ident<KeyName>	(.Counter+=print("KeyWord {0}: {1}",Counter,KeyName).)
		"="
		String<StringValue>
		"."

	}

.


Tokens =				(.TokenName = ' '
					  Counter = 0.)
	
	"TOKENS"			(.print("LEYENDO TOKENS").)
	{
		Ident<TokenName>	(.Counter+=print("Token {0}: {1}",Counter,TokenName).)
		"="
		TokenExpr
		[ExceptKeyword]
		"."

	}

.


Productions = 				(.Counter = 0.)
	
	"PRODUCTIONS"			(.ProdName = ' '
					  print("LEYENDO PRODUCTIONS").)
	{
		Ident<ProdName>	(.Counter+=print("Production {0}: {1}",Counter,ProdName).)
		[Atributos]
		"="
		[Codigo]
		ProductionExpr
		"."

	}
.

ExceptKeyword= 
	"EXCEPT"
	"KEYWORDS"
.


ProductionExpr =
	ProdTerm
	{
	"|"
	ProdTerm
	}
.

ProdTerm =
	ProdFactor
	{
	ProdFactor
	}
.

ProdFactor =
	(
	SymbolProd
	|
	"("ProductionExpr")"
	|
	"["ProductionExpr"]"
	|
	"{"ProductionExpr"}"
	)
	[Codigo]
.



SymbolProd =			(.SV = ' '
				  IN = ' '.)
	(
	String<SV>		(.print("en Production: {0}",SV).)
	|
	char
	|
	Ident<IN>		(.print("Identificador en Production: {0}",IN).)
	[Atributos] 
	)
.

Codigo =
	startcode
	{ ANY }
	endcode
.

Atributos =
	"<"
	{ANY}
	">"
.
	


TokenExpr=
	TokenTerm
	{
	"|"
	TokenTerm
	}
.

TokenTerm=
	TokenFactor
	{
	TokenFactor	
	}
.

TokenFactor=
	(
	SimbolToken
	|
	"("TokenExpr")"
	|
	"["TokenExpr"]"
	|
	"{"TokenExpr"}"
	)
.

SimbolToken =			(.IdentName = ' '
				  StringValue = ' '.)		
	(
	String<StringValue>
	|
	char
	|
	Ident<IdentName>	(.print("Identificador en Token: {0}",IdentName).)
	)
.



CharSet= 			(.IdentName = ' '
				  StringValue = ' '.)	
	(
	String<StringValue>
	|
	Char
	|
	"ANY"
	|
	Ident<IdentName>	(.print("Identificador en CharSet: {0}",IdentName).)
	)
	
.


Char=
	(
	char
	|
	charnumber
	|
	charinterval
	)
.


String<S>=
	string		(.S = LastToken.Value.)
.

Ident<S>=
	ident		(.S = LastToken.Value.)
.


END MyCOCOR
COMPILER MyCOCOR
(.//Especificacion de Funciones.)
CHARACTERS

letter= "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
digit= "0123456789".

cr = CHR(13).
lf = CHR(10).
tab = CHR(9).
ignore = cr+lf+tab.
comillas = CHR(34).
stringletter = ANY-comillas-ignore.

operadores ="+-=()[]{}|.<>".
MyANY = ANY-operadores.

TOKENS

ident = letter{letter|digit} EXCEPT KEYWORDS.
string = comillas stringletter{stringletter} comillas.

char = "'"["/"]letter"'".
charnumber = "CHR("digit{digit}")".
charinterval = "CHR("digit{digit}")""..""CHR("digit{digit}")".

nontoken = MyANY.
startcode ="(.".
endcode=".)".

IGNORE SET ignore

PRODUCTIONS

MyCOCOR= 				(.string CompilerName = new string(' ',0);
					  string EndName = new string(' ',0);.)
	"COMPILER" 
	Ident<ref CompilerName>		(.print("Nombre Inicial del Compilador:{0}",CompilerName);.) 
	[Codigo]
	Body
	"END" Ident<ref EndName>	(.print("Nombre Final del Compilador:{0}",EndName);.) 
.

Body = 
	Characters 
	[Keywords] 
	Tokens 
	Productions  	
.


Characters =				(.string CharName = new string(' ',0);
					   int Counter = 0;.)

	"CHARACTERS"			(.print("LEYENDO CHARACTERS");.)
	{
		Ident<ref CharName>	(.Counter++;print("Char Set {0}: {1}",Counter,CharName);.)
		"="
		CharSet			
		{
			"+"CharSet
			|
			"-"CharSet
		}
		"."
	}
.
			 
Keywords = 				(.string KeyName = new string(' ',0);
					  string StringValue = new string(' ',0);
    					  int Counter = 0;.)

	"KEYWORDS"			(.print("LEYENDO KEYWORDS");.)
	{
		
		Ident<ref KeyName>	(.Counter++;print("KeyWord {0}: {1}",Counter,KeyName);.)
		"="
		String<ref StringValue>
		"."

	}

.


Tokens =				(.string TokenName = new string(' ',0);
					  int Counter = 0;.)
	
	"TOKENS"			(.print("LEYENDO TOKENS");.)
	{
		Ident<ref TokenName>	(.Counter++;print("Token {0}: {1}",Counter,TokenName);.)
		"="
		TokenExpr
		[ExceptKeyword]
		"."

	}

.


Productions = 				(.int Counter = 0;.)
	
	"PRODUCTIONS"			(.string ProdName = new string(' ',0);
					  print("LEYENDO PRODUCTIONS");.)
	{
		Ident<ref ProdName>	(.Counter++;print("Production {0}: {1}",Counter,ProdName);.)
		[Atributos]
		"="
		[Codigo]
		ProductionExpr
		"."

	}
.

ExceptKeyword= 
	"EXCEPT"
	"KEYWORDS"
.


ProductionExpr =
	ProdTerm
	{
	"|"
	ProdTerm
	}
.

ProdTerm =
	ProdFactor
	{
	ProdFactor
	}
.

ProdFactor =
	(
	SymbolProd
	|
	"("ProductionExpr")"
	|
	"["ProductionExpr"]"
	|
	"{"ProductionExpr"}"
	)
	[Codigo]
.



SymbolProd =			(.string SV = new string(' ',0);
				  string IN = new string(' ',0);.)
	(
	String<ref SV>		(.print("String en Production: {0}",SV);.)
	|
	char
	|
	Ident<ref IN>		(.print("Identificador en Production: {0}",IN);.)
	[Atributos] 
	)
.

Codigo =
	startcode
	{ ANY }
	endcode
.

Atributos =
	"<"
	{ANY}
	">"
.
	


TokenExpr=
	TokenTerm
	{
	"|"
	TokenTerm
	}
.

TokenTerm=
	TokenFactor
	{
	TokenFactor	
	}
.

TokenFactor=
	(
	SimbolToken
	|
	"("TokenExpr")"
	|
	"["TokenExpr"]"
	|
	"{"TokenExpr"}"
	)
.

SimbolToken =			(.string IdentName = new string(' ',0);
				  string StringValue = new string(' ',0);.)		
	(
	String<ref StringValue>
	|
	char
	|
	Ident<ref IdentName>	(.print("Identificador en Token: {0}",IdentName);.)
	)
.



CharSet= 			(.string IdentName = new string(' ',0);
				  string StringValue = new string(' ',0);.)	
	(
	String<ref StringValue>
	|
	Char
	|
	"ANY"
	|
	Ident<ref IdentName>	(.print("Identificador en CharSet: {0}",IdentName);.)
	)
	
.


Char=
	(
	char
	|
	charnumber
	|
	charinterval
	)
.


String<ref string S>=
	string		(.S = LastToken.Value;.)
.

Ident<ref string S>=
	ident		(.S = LastToken.Value;.)
.


END MyCOCOR